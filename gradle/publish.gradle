static void configurePublishing(Project project, String id) {
    project.with {
        apply plugin: 'maven-publish'
        apply plugin: 'com.jfrog.artifactory'
        artifactoryPublish.skip = true

        plugins.withId('java') {

            artifactory {
                contextUrl = "https://artifactory.glovoint.com/artifactory/"
                publish {
                    repository {
                        repoKey = 'gradle-release-local'
                        username = project.findProperty('artifactory_user')
                        password = project.findProperty('artifactory_key')
                        maven = true

                    }
                    defaults {
                        publications('mavenJava')
                        publishArtifacts true
                        version = "0.0.1"
                    }
                }
                resolve {
                    repository {
                        repoKey = 'gradle-release'
                        username = project.findProperty('artifactory_user')
                        password = project.findProperty('artifactory_key')
                        maven = true
                    }
                }
            }
            publishing {
                publications {
                    mavenJava(MavenPublication) {
                        groupId = 'com.glovo.gradle'
                        artifactId = id
                        version = "0.0.1"
                        from(components.java)
                    }
                }
            }
        }
    }
}

configurePublishing(project(':plugin:core'), 'android-version-plugin')
configurePublishing(project(':plugin:kotlin-extensions'), 'android-version-plugin-kotlin')
